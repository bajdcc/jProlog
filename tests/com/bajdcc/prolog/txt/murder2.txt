/**
 * 题目：祸起萧墙
 * 一天晚上，在一个由一对夫妇和他们的儿子女儿组成的四口之家中，发生了一起谋杀案。家庭中的一个成员杀害了另一个成员；其他两个成员一个是目击者，另一个是凶手的同谋。
 * (1)同谋和目击者的性别不同
 * (2)最年长的成员和目击者的性别不同
 * (3)最年轻的成员和被害者的性别不同
 * (4)同谋的年龄比被害者大
 * (5)父亲是最年长的成员
 * (6)凶手不是最年轻的成员
 * 请问谁是凶手
 */


设置("版权", "bajdcc");
设置("定时", 1000);

//变量定义部分
所有人{} = {"父亲","母亲","儿子","女儿"};
性别{} = {"男","女"};
年龄{} = {1,2,3,4}; 
年龄 year(所有人);
性别 sex(所有人);
所有人 最年长,最年轻,凶手,同谋,目击者,死者;

//条件部分
sex("父亲") = "男";
sex("母亲") = "女";
sex("儿子") = "男";
sex("女儿") = "女";
year("父亲") > year("儿子");
year("父亲") > year("女儿");
year("母亲") > year("儿子");
year("母亲") > year("女儿");

所有人 身份[]; //由于凶手,同谋,目击者,死者必须互不相同，可以使用一个序变了描述否则只能写成下面这样
//凶手 != 同谋; 凶手 != 目击者; 凶手 != 死者;
//同谋 != 目击者; 同谋 != 死者; 目击者 != 死者;
身份[1] = 凶手; 身份[2] = 同谋; 身份[3] = 目击者; 身份[4] = 死者;

@ 任意 x 属于 所有人, x!=最年长 -> year(最年长) > year(x); //最年长的定义
@ 任意 x 属于 所有人, x!=最年轻 -> year(最年轻) < year(x); //最年轻的定义
sex(同谋) != sex(目击者); //(1)
sex(最年长) != sex(目击者); //(2)
sex(最年轻) != sex(死者); //(3)
year(同谋) > year(死者); //4
最年长 = "父亲"; //5
最年轻 != 凶手; //6

输出("凶手是: ", 凶手);